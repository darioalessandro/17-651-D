//---------------------------------------------------
//  Simple Infusion Pump
//---------------------------------------------------
//
// Set of actions that can be selected interactively to
// the animation of this model with the LTSA tool.
//
menu AnimationControlMenu = {
    plug_in, set_value[0..3], reset, fill_fluids
}
//---------------------------------------------------
//======================
// Constants and Ranges
//======================
const Max = 3 range Amt = 0 .. Max
const FillAmt = 2    // Amount in bag initially and after refilling
//=====================
// Process Definitions
//=====================
//
// Pump starts in power off state
//
PUMP = POWER_OFF,
//
// User must plug pump in before anything else can happen
//
POWER_OFF = (
    plug_in -> SETUP
),
//
// Before pump operation starts, user must enter amount of medicine to deliver
// to patient
//
SETUP = (
    set_value[deliver:Amt] -> PUMP[deliver][FillAmt]
),
//
// Main operation of pump:
//  User may reset pump at any time
//  When the pump has delivered the amount of medicine requested it goes
//      to the DONE state
//  When fluid runs out, the pump goes into an alarm state
//  Otherwise, the pump delivers one unit of medicine
//
PUMP[deliver:Amt][remaining:Amt] = (
    reset -> SETUP
    |
    when (deliver == 0)
done -> DONE |
    when (remaining == 0)
        fluid_empty -> EMPTY_ALARM[deliver]
    |
    when (deliver > 0 && remaining > 0)
        pump_fluid -> PUMP[deliver-1][remaining-1]
),
//
// Error state associated with empty pump:
//  Repeatedly rings bell until user refills the pump
//
EMPTY_ALARM[deliver:Amt] = (
    ring_bell -> EMPTY_ALARM[deliver]
    |
    fill_fluids -> PUMP[deliver][FillAmt]
),
DONE = END.